#
# Car-Routing is experimantal !!!
# DO NOT USE FOR ACTUAL NAVIGATION
# Turn restrictions are missing, leading to wrong routes
# Based on standard Car-test, added speed nad eco preferences to roads.

---context:global

assign downhillcost 0
assign downhillcutoff 0
assign uphillcost 7 # ( 1500 kg, 50 kW engine climbs about 14 km/h )
assign uphillcutoff 0

assign validForCars 1
assign pass1coefficient 1.3

# drivestyle 0=short	-	Shortest way possible ( for completness, not reaslly useful )
# drivestyle 1=eco		-	Least estimated fuel consumption
# drivestyle 2=fasteco	-	Balanced fuel consuption and travel time ( default )
# drivestyle 3=fast		-   Least travel time, based on lower of explicit maxspeed or defined nominal speed.

assign drivestyle 2

assign short 	equal drivestyle 0
assign eco 		equal drivestyle 1
assign ecofast equal drivestyle 2
assign fast    equal drivestyle 3

assign avoid_motorways	0 # either toll either free ones
assign avoid_toll		0 # avoid paid ways 

---context:way   # following code refers to way-tags

assign turncost if junction=roundabout then 0 else 200
assign initialcost switch route=ferry 20000 0


#
# calculate logical car access
#
assign caraccess
       switch motorcar=
              switch motor_vehicle=
                     switch vehicle=
                            switch access=
                                   switch or highway=motorway highway=motorway_link
												switch avoid_motorways 0 switch and avoid_toll toll=yes 0 1
                                   switch or highway=trunk highway=trunk_link          1
                                   switch or highway=primary highway=primary_link      1
                                   switch or highway=secondary highway=secondary_link  1
                                   switch or highway=tertiary highway=tertiary_link    1
                                   switch    highway=unclassified                      1
                                   switch    route=ferry                               1
                                   switch or highway=residential highway=living_street 1
                                   switch    highway=service                           1
                                   0
                                   or access=yes or access=permissive or access=designated access=destination
                            or vehicle=yes or vehicle=designated vehicle=destination
                     or motor_vehicle=yes or motor_vehicle=permissive or motor_vehicle=designated motor_vehicle=destination
              or motorcar=yes or motorcar=permissive or motorcar=designated motorcar=destination

assign accesspenalty
       switch caraccess
              0
              10000

assign onewaypenalty
       switch switch reversedirection=yes
                     switch oneway=
                            junction=roundabout
                            or oneway=yes or oneway=true oneway=1
                     oneway=-1
              10000
              0.0



assign ispaved or surface=paved or surface=asphalt or surface=concrete surface=paving_stones

assign costfactor

 add max onewaypenalty accesspenalty

 switch and highway= not route=ferry  100000

 # original values from car-test profile
 # switch or highway=motorway highway=motorway_link    1
 # switch or highway=trunk highway=trunk_link          1
 # switch or highway=primary highway=primary_link      switch maxspeed=30 2.0 switch maxspeed=50 1.5 1.3
 # switch or highway=secondary highway=secondary_link  1.4
 # switch or highway=tertiary highway=tertiary_link    1.5
 # switch    highway=unclassified                      1.6
 # switch    route=ferry                               5.67
 # switch    highway=bridleway                         5
 # switch or highway=residential highway=living_street 2
 # switch    highway=service                           2
 # switch or highway=track or highway=road highway=path
 #  switch tracktype=grade1 5
 #  switch ispaved 5
 #  30
 # 10000
 
# costfactors are trivially 1.0 for shortest route
# For Fast route, they are reciprocal to chosen nominal or explicit speed, with 1.0 for 130 km/h, usual for EU motorways
# for Eco route, they are proportional to estimated specific fuel consumption, derived from http://www.mpgforspeed.com
# for FastEco, they are weighted from Eco and Fast

# 	Short	Fast	Eco	FastEco
# kmph				0.7
# 10	1.00	13.00	2.49	5.81
# 15	1.00	8.67	1.90	3.99
# 20	1.00	6.50	1.57	3.07
# 25	1.00	5.20	1.34	2.50
# 30	1.00	4.33	1.18	2.12
# 40	1.00	3.25	1.07	1.69
# 50	1.00	2.60	1.03	1.44
# 60	1.00	2.17	1.02	1.28
# 70	1.00	1.86	1.00	1.16
# 80	1.00	1.63	1.00	1.08
# 90	1.00	1.44	1.01	1.02
# 100	1.00	1.30	1.05	1.00
# 110	1.00	1.18	1.17	1.02
# 120	1.00	1.08	1.29	1.05
# 130	1.00	1.00	1.40	1.08


#avoiding strange detours sometimes observed in OSMAnd native navigation
#related to implict/explicit speed limits, so links may appear faster.

add switch 	highway=motorway_link|trunk_link|primary_link|secondary_link|tertiary_link 0.8 0	
	        	
if short then 1.0 # if someone insists on shortest route, what does not make much sense for cars

else (  
	if maxspeed=10					then ( if eco then 2.49 else if ecofast then 5.81 else 13.0 ) else
	if maxspeed=20					then ( if eco then 1.57 else if ecofast then 3.07 else 6.50 ) else
	if maxspeed=30					then ( if eco then 1.18 else if ecofast then 2.12 else 4.33 ) else
	if maxspeed=40					then ( if eco then 1.07 else if ecofast then 1.69 else 3.25 ) else
	if maxspeed=50					then ( if eco then 1.03 else if ecofast then 1.44 else 2.60 ) else
	if maxspeed=urban					then ( if eco then 1.03 else if ecofast then 1.44 else 2.60 ) else
	if highway=unclassified				then ( if eco then 1.03 else if ecofast then 1.44 else 2.60 ) else
	if maxspeed=60					then ( if eco then 1.02 else if ecofast then 1.28 else 2.17 ) else
	if highway=tertiary|tertiary_link		then ( if eco then 1.02 else if ecofast then 1.28 else 2.17 ) else
	if maxspeed=70					then ( if eco then 1.00 else if ecofast then 1.16 else 1.86 ) else
	if highway=secondary|secondary_link		then ( if eco then 1.00 else if ecofast then 1.16 else 1.86 ) else
	if maxspeed=80					then ( if eco then 1.00 else if ecofast then 1.08 else 1.63 ) else
	if maxspeed=90					then ( if eco then 1.01 else if ecofast then 1.02 else 1.44 ) else
	if maxspeed=rural					then ( if eco then 1.01 else if ecofast then 1.02 else 1.44 ) else
	if highway=primary|primary_link		then ( if eco then 1.01 else if ecofast then 1.02 else 1.44 ) else
	if maxspeed=100					then ( if eco then 1.05 else if ecofast then 1.00 else 1.30 ) else
	if maxspeed=110					then ( if eco then 1.17 else if ecofast then 1.02 else 1.18 ) else
	if highway=trunk|trunk_link			then ( if eco then 1.17 else if ecofast then 1.02 else 1.18 ) else
	if maxspeed=120					then ( if eco then 1.29 else if ecofast then 1.05 else 1.08 ) else
	if maxspeed=130					then ( if eco then 1.40 else if ecofast then 1.08 else 1.00 ) else
	if highway=motorway|motorway_link		then ( if eco then 1.40 else if ecofast then 1.08 else 1.00 ) else
	
	if route=ferry             		then 5.67 else 

	if highway=bridleway 				then ( if eco then 1.57 else if ecofast then 3.07 else 6.50 ) else # 20
	if highway=residential|service		then ( if eco then 1.03 else if ecofast then 1.44 else 2.60 ) else # 50
	if highway=living_street 			then ( if eco then 1.18 else if ecofast then 2.12 else 4.33 ) else # 30
	if highway=track|road|path    		then 
	   if or tracktype=grade1 ispaved   	then ( if eco then 1.57 else if ecofast then 3.07 else 6.50 )  # 20
								else  30
      else 10000
)
	



---context:node  # following code refers to node tags

#
# calculate logical car access to nodes
#
assign caraccess
       switch motorcar=
              switch motor_vehicle=
                     switch vehicle=
                            switch access=
                                   switch barrier=gate 0
                                   switch barrier=bollard 0
                                   switch barrier=lift_gate 0
                                   switch barrier=cycle_barrier 0
                                   switch and barrier=toll_booth avoid_toll 0
                                   switch and highway=toll_bridge avoid_toll 0
                                   1
                                   or access=yes or access=permissive or access=designated access=destination
                            or vehicle=yes or vehicle=permissive or vehicle=designated vehicle=destination
                     or motor_vehicle=yes or motor_vehicle=permissive or motor_vehicle=designated motor_vehicle=destination
              or motorcar=yes or motorcar=permissive or motorcar=designated motorcar=destination

assign initialcost
       switch caraccess
              0
              1000000
